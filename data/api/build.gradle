plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

android {
    compileSdk buildVersions.compileSdkVersion

    defaultConfig {
        minSdk buildVersions.minSdkVersion
        targetSdk buildVersions.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "OPENWEATHER_KEY", "\"" + getAccessKey('OPENWEATHER_KEY') + "\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'com.alvindizon.panahon.api'
}

dependencies {
    implementation project(":data:networking")
    implementation deps.network.retrofit.retrofit
    implementation deps.network.retrofit.moshiConverter

    implementation deps.coroutines.core

    implementation deps.network.moshi.moshi
    kapt deps.network.moshi.codegen

    implementation deps.dagger.hiltAndroid
    kapt deps.dagger.hiltCompiler
}

String getAccessKey(String keyName) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def applicationKey = properties.getProperty(keyName)
    if (applicationKey == null || "" == applicationKey) {
        throw new IllegalStateException("Set your ${keyName} access key in local.properties file.")
    }
    return applicationKey
}
